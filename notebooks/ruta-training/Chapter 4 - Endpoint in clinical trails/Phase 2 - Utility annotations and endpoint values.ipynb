{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Phase 2 - Utility annotations and endpoint values\n",
    "\n",
    "In this notebook, we define and create some annotations that could be useful for the intented task."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "First, we fill some wordlists/dictionaries for indicator words like \"ORR\", \"survival rate\" and \"PFS\". If no rows are created for the output, then we have at least found one indicator in a sentence with an endpoint, which is ok for the start."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "scrolled": false
   },
   "outputs": [],
   "source": [
    "%inputDir data-nlp\n",
    "%displayMode CSV\n",
    "%csvConfig MissingIndSentence\n",
    "\n",
    "TYPESYSTEM TrialsTypeSystem;\n",
    "TYPESYSTEM DKProCoreTypeSystem;\n",
    "\n",
    "DECLARE EndpointInd;\n",
    "DECLARE EndpointInd ORRInd, OSInd, PFSInd;\n",
    "WORDLIST orrIndList = \"orr_ind.txt\";\n",
    "WORDLIST osIndList = \"os_ind.txt\";\n",
    "WORDLIST pfsIndList = \"pfs_ind.txt\";\n",
    "\n",
    "MARKFAST(ORRInd, orrIndList, true);\n",
    "MARKFAST(OSInd, osIndList, true);\n",
    "MARKFAST(PFSInd, pfsIndList, true);\n",
    "\n",
    "// hotfix sentences, broken char is a question mark\n",
    "s1:Sentence{ENDSWITH(QUESTION)} s2:@Sentence{->UNMARK(s1),s2.begin=s1.begin};\n",
    "\n",
    "DECLARE MissingIndSentence;\n",
    "Sentence{CONTAINS(TrialsEntity),-CONTAINS(EndpointInd)-> MissingIndSentence};\n",
    "\n",
    "COLOR(ORR, \"#F07C62\");\n",
    "COLOR(OSMean, \"#65FF5B\");\n",
    "COLOR(OSTime, \"#51C849\");\n",
    "COLOR(OSRate, \"#23C318\");\n",
    "COLOR(PFSMean, \"#788AFF\");\n",
    "COLOR(PFSTime, \"#707BC3\");\n",
    "COLOR(PFSRate, \"#0020F5\");\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Next, we try to detect numeric values by comparing the output of our rules to POS_NUM annotations created by CoreNLP."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%resetCas\n",
    "%inputDir data-nlp\n",
    "%outputDir ./temp/num-out\n",
    "%displayMode CSV\n",
    "%csvConfig MissingNumSentence\n",
    "\n",
    "TYPESYSTEM TrialsTypeSystem;\n",
    "TYPESYSTEM DKProCoreTypeSystem;\n",
    "\n",
    "TYPE RutaNUM = org.apache.uima.ruta.type.NUM;\n",
    "DOUBLE num;\n",
    "\n",
    "DECLARE NumericValue (DOUBLE value, DOUBLE min, DOUBLE max);\n",
    "\n",
    "// add annotate word that represent numbers\n",
    "WORDTABLE NumberTable = \"numbers.csv\";\n",
    "MARKTABLE(NumericValue, 2, NumberTable, true, 2, \"\", 2, \"value\" = 1);\n",
    "\n",
    "\n",
    "ADDRETAINTYPE(WS);\n",
    "// normal numbers like 1,000.95\n",
    "(RutaNUM{-PARTOF(NumericValue)} (COMMA RutaNUM{REGEXP(\"...\")}) \n",
    "    (PERIOD RutaNUM)?){PARSE(num, \"en\")-> nv:NumericValue, nv.value=num};\n",
    "(RutaNUM{-PARTOF(NumericValue)} (PERIOD RutaNUM)?){PARSE(num, \"en\")-> nv:NumericValue, nv.value=num};\n",
    "(PERIOD{-PARTOF(NumericValue)} RutaNUM){PARSE(num, \"en\")-> nv:NumericValue, nv.value=num};\n",
    "\n",
    "// combined numbers like twenty-two\n",
    "(nv1:NumericValue{PARTOF(W)-> UNMARK(nv1)} \n",
    "    SPECIAL.ct==\"-\" \n",
    "    nv2:NumericValue{PARTOF(W)-> UNMARK(nv2)}){-> nv:NumericValue, nv.value = (nv1.value+nv2.value)};\n",
    "// intervals like 39-54\n",
    "(nv1:NumericValue{-> UNMARK(nv1)} SPECIAL?\n",
    "    SPECIAL.ct==\"-\" \n",
    "    nv2:@NumericValue{-> UNMARK(nv2)}){-> new:NumericValue, new.min=nv1.value, new.max=nv2.value};\n",
    "REMOVERETAINTYPE(WS);\n",
    "\n",
    "DECLARE MissingNumSentence, MissingNum;\n",
    "POS_NUM{-IS(NumericValue)->MissingNum};\n",
    "\n",
    "// we ignore some in the reporting\n",
    "// no roman numbers\n",
    "m:MissingNum{IS(CAP) -> UNMARK(m)};\n",
    "m:MissingNum{IS(CW), REGEXP(\".\") -> UNMARK(m)};\n",
    "// no 1990s\n",
    "m:MissingNum{STARTSWITH(RutaNUM),ENDSWITH(SW) -> UNMARK(m)};\n",
    "// no slashes or fractions ... for now\n",
    "m:MissingNum{ -> UNMARK(m)}<-{SPECIAL{REGEXP(\"[+/]\")};COLON;};\n",
    "// no negative numbers\n",
    "m:MissingNum{STARTSWITH(SPECIAL) -> UNMARK(m)}<-{SPECIAL{REGEXP(\"[-]\")};};\n",
    "\n",
    "Sentence{CONTAINS(MissingNum)-> MissingNumSentence};\n",
    "\n",
    "COLOR(NumericValue, \"lightgreen\");\n",
    "COLOR(MissingNum, \"pink\");"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We investigate the values of some NumericValue annotations. The cell before needs to be evaluated for the type systems."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "%loadCas temp/num-out/18720480.txt.xmi\n",
    "%displayMode DYNAMIC_HTML"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The numeric value are not good, but good enough for now concerning the endpoints. Next, we try to detect percentages and durations, the values of the endpoints. If the output generates no rows, then we are able to annotate a value for the corresponding endpoint."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "scrolled": false
   },
   "outputs": [],
   "source": [
    "%resetCas\n",
    "%inputDir data-nlp\n",
    "%outputDir ./temp/value-out\n",
    "%displayMode CSV\n",
    "%csvConfig MissingValueSentence\n",
    "//%saveTypeSystem ./TypeSystem.xml\n",
    "\n",
    "TYPESYSTEM TrialsTypeSystem;\n",
    "TYPESYSTEM DKProCoreTypeSystem;\n",
    "\n",
    "DECLARE TimeInd (STRING kind);\n",
    "DECLARE NumericValue (DOUBLE value, DOUBLE min, DOUBLE max, DOUBLE var);\n",
    "DECLARE Unit (STRING kind);\n",
    "DECLARE Value (NumericValue value, Unit unit);\n",
    "\n",
    "// hotfix sentences\n",
    "s1:Sentence{ENDSWITH(QUESTION)} s2:@Sentence{->UNMARK(s1),s2.begin=s1.begin};\n",
    "\n",
    "// copied rules from above\n",
    "TYPE RutaNUM = org.apache.uima.ruta.type.NUM;\n",
    "DOUBLE num;\n",
    "WORDTABLE NumberTable = \"numbers.csv\";\n",
    "MARKTABLE(NumericValue, 2, NumberTable, true, 2, \"\", 2, \"value\" = 1);\n",
    "\n",
    "BLOCK(NumericValues) Document{}{\n",
    "    // normal numbers like 1,000.95\n",
    "    ADDRETAINTYPE(WS);\n",
    "    (RutaNUM{-PARTOF(NumericValue)} (COMMA RutaNUM{REGEXP(\"...\")}) \n",
    "        (PERIOD RutaNUM)?){PARSE(num, \"en\")-> nv:NumericValue, nv.value=num};\n",
    "    (RutaNUM{-PARTOF(NumericValue)} (PERIOD RutaNUM)?){PARSE(num, \"en\")-> nv:NumericValue, nv.value=num};\n",
    "    (PERIOD{-PARTOF(NumericValue)} RutaNUM){PARSE(num, \"en\")-> nv:NumericValue, nv.value=num};\n",
    "\n",
    "    // like twenty-two\n",
    "    (nv1:NumericValue{PARTOF(W)-> UNMARK(nv1)} \n",
    "        SPECIAL.ct==\"-\" \n",
    "        nv2:NumericValue{PARTOF(W)-> UNMARK(nv2)}){-> nv:NumericValue, nv.value = (nv1.value+nv2.value)};\n",
    "    // intervals like 39-54\n",
    "    (nv1:NumericValue{-> UNMARK(nv1)} SPECIAL?\n",
    "        SPECIAL.ct==\"-\" \n",
    "        nv2:@NumericValue{-> UNMARK(nv2)}){-> new:NumericValue, new.min=nv1.value, new.max=nv2.value};\n",
    "    \n",
    "    // NEW: we also need to detect variance like 3+/-0.4\n",
    "    (nv1:@NumericValue{-> nv1.var=nv2.value,nv1.end=nv2.end} \"+/-\" nv2:NumericValue{-> UNMARK(nv2)});\n",
    "    \n",
    "    REMOVERETAINTYPE(WS);\n",
    "}\n",
    "\n",
    "// now to the actual rules\n",
    "\n",
    "// indicators for durations like months\n",
    "WORDTABLE TimeIndTable = \"time_ind.csv\";\n",
    "MARKTABLE(TimeInd, 1, TimeIndTable, \"kind\"=2);\n",
    "\n",
    "// something that could hint an arm\n",
    "DECLARE ArmInd;\n",
    "// we should probably refactor this to a dictionary\n",
    "(W{REGEXP(\"arm\", true)} W{REGEXP(\"[abc]\", true)} RutaNUM? COLON?){-> ArmInd};\n",
    "\n",
    "// ignore text in brackets\n",
    "DECLARE InBrackets;\n",
    "(SPECIAL.ct==\"(\" ANY.ct!=\")\"[1,25] SPECIAL.ct==\")\"){-> InBrackets};\n",
    "ADDFILTERTYPE(InBrackets);\n",
    "\n",
    "// annotate the acutal Value\n",
    "// 10%\n",
    "(nv:NumericValue SPECIAL.ct==\"%\"{-> u:Unit,u.kind=\"percent\"}){-> v:Value, v.value=nv, v.unit=u};\n",
    "// 12 months\n",
    "(nv:NumericValue SPECIAL.ct==\"-\"? ti:TimeInd{-> u:Unit,u.kind=ti.kind}){-> v:Value, v.value=nv, v.unit=u};\n",
    "\n",
    "// chunks that could be an arm indicator\n",
    "Value (POS_ADP{-REGEXP(\"in\")} W[1,2]{-PARTOF(TimeInd),-PARTOF(POS_CONJ),-PARTOF(NumericValue)}){-> ArmInd};\n",
    "(POS_ADP{-REGEXP(\"in\")} W[1,2]{-PARTOF(TimeInd),-PARTOF(POS_CONJ),-PARTOF(NumericValue)}){-> ArmInd} POS_CONJ @Value;\n",
    "\n",
    "// now some additional logic for combined mentions\n",
    "DECLARE Enum;\n",
    "DECLARE VSInd;\n",
    "// we could add a wordlist dictionary, but for new we simple classify the words\n",
    "(W{REGEXP(\"v|vs|versus\")} PERIOD?){-> VSInd};\n",
    "\n",
    "// 25 vs. 8%\n",
    "(nv1:NumericValue{-PARTOF(Value)-> v:Value, v.value=nv1, v.unit=v2.unit}\n",
    "    VSInd v2:Value){-> Enum};\n",
    "// 2, 3, and 4 months\n",
    "((NumericValue{-PARTOF(Value) -> v:Value, Value.value=NumericValue, Value.unit=v2.unit} COMMA?)+ \n",
    "    W{REGEXP(\"and\")} v2:@Value){->Enum};\n",
    "// 2- and 3 months\n",
    "((nv1:NumericValue{-PARTOF(Value)} SPECIAL.ct==\"-\"?){-> v:Value, v.value=nv1, v.unit=v2.unit}\n",
    "    W{REGEXP(\"and\")} v2:Value){->Enum};\n",
    "\n",
    "// no unit? like \"was 0.89\"\n",
    "W{REGEXP(\"was\")} nv:@NumericValue{-PARTOF(Value), nv.value > 0, nv.value < 1 -> u:Unit, u.kind=\"percent\", v:Value, v.value=nv, v.unit=u};\n",
    "\n",
    "// even more distant combinations\n",
    "ADDFILTERTYPE(ArmInd,COMMA,POS_CONJ);\n",
    "v:Value nv:NumericValue{-PARTOF(Value)-> new:Value, new.value=nv, new.unit=v.unit};\n",
    "nv:NumericValue{-PARTOF(Value)-> new:Value, new.value=nv, new.unit=v.unit} v:@Value ;\n",
    "\n",
    "// some clean up of false positives\n",
    "DECLARE NoValueContextInd;\n",
    "W{REGEXP(\"patients?\", true)->NoValueContextInd};\n",
    "v:Value{-> UNMARK(v)} NoValueContextInd;\n",
    "\n",
    "DECLARE MissingValueSentence, MissingValue;\n",
    "TrialsEntity{-IS(Value)-> MissingValue};\n",
    "Sentence{CONTAINS(MissingValue)-> MissingValueSentence};\n",
    "\n",
    "//COLOR(NumericValue, \"#F0F050\");\n",
    "COLOR(ArmInd, \"#F0F0A0\");\n",
    "COLOR(InBrackets, \"#F0F0F0\");\n",
    "COLOR(MissingValue, \"red\");\n",
    "COLOR(Value, \"lightgreen\");\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We have found a value for every endpoint span and potientially also false positves. Now, we simply plot the Values to get an overview what we actually extract and can again finetune some rules above if neccessary."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%inputDir ./temp/value-out\n",
    "%outputDir ./temp/trash\n",
    "%displayMode CSV\n",
    "%csvConfig Value unit.kind value.value value.min value.max value.var   "
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "UIMA Ruta",
   "language": "ruta",
   "name": "ruta"
  },
  "language_info": {
   "codemirror_mode": "ruta",
   "file_extension": ".ruta",
   "mimetype": "text/ruta",
   "name": "UIMA Ruta",
   "pygments_lexer": "ruta",
   "version": "3.0.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
