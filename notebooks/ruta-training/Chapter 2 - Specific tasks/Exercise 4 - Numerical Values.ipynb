{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Exercise 4: Numerical Values\n",
    "\n",
    "The goal of this exercise is to create a script for detecting and normalizing numerical values like \"1/4\" or \"thirty one\". We declare a new annotation type named \"NumericValue\" with a feature \"value\" of the type DOUBLE. The input document contains two numerical expressions in each line. The first one represents the text passage that should be annotated with \"NumericalValue\" and the second one represents the value of feature \"value\". We try to annotate as many as possible with the correct feature value. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%documentText\n",
    "101;101;\n",
    "2.3;2.3;\n",
    "3,000;3000;\n",
    "3^2;9;\n",
    "3.3e3;3300;\n",
    "1/4;0.25;\n",
    "9^1/2;3;\n",
    "4x10^3;4000;\n",
    "5.5*4^5;5632;\n",
    "thirty one;31;\n",
    "three hundred;300;\n",
    "four thousand one hundred and two;4102;\n",
    "3 million;3000000;\n",
    "fünfundzwanzig;25;\n",
    "½ million;500000;\n",
    "4²;16;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%displayMode DYNAMIC_HTML\n",
    "\n",
    "WORDTABLE NumberTable = 'Numbers.csv';\n",
    "WORDTABLE MultiplicatorTable = 'Multiplicators.csv';\n",
    "WORDTABLE SpecialChararactorFractionTable = 'SpecialCharacterFractions.csv';\n",
    "WORDTABLE SpecialCharacterTable = 'SpecialCharacters.csv';\n",
    "WORDTABLE ExponentTable = 'Exponents.csv';\n",
    "\n",
    "DECLARE NumericValue (DOUBLE value);\n",
    "DECLARE Fraction(NumericValue numerator, NumericValue denominator);\n",
    "DECLARE SimpleFraction(INT numerator, INT denominator);\n",
    "\n",
    "DECLARE ConjunctionWord;\n",
    "DECLARE NormNum (DOUBLE value);\n",
    "DECLARE NormNum NumInd, MultiplicatorInd, ExponentInd;\n",
    "\n",
    "BOOLEAN allowPeriodDecimalSeparator = true;\n",
    "STRING decimalSeparatorString = \"\\\\.\";\n",
    "STRING thousandsSeparatorString = \",\";\n",
    "STRING language = \"en\";\n",
    "\n",
    "DECLARE LanguageContainer (STRING language);\n",
    "Document{IS(DocumentAnnotation)-> GETFEATURE(\"language\", language)};\n",
    "LanguageContainer{-> GETFEATURE(\"language\", language)};\n",
    "\n",
    "DECLARE DecimalSeparator, ThousandsSeparator;\n",
    "\n",
    "\"and|und|et\" -> ConjunctionWord;\n",
    "\n",
    "FOREACH(pm) PM {} {\n",
    "    pm{REGEXP(decimalSeparatorString) -> DecimalSeparator};\n",
    "    pm{REGEXP(thousandsSeparatorString) -> ThousandsSeparator};\n",
    "}\n",
    "\n",
    "\n",
    "MARKTABLE(NumericValue, 2, NumberTable, true, 2, \"\", 2, \"value\" = 1);\n",
    "MARKTABLE(MultiplicatorInd, 2, MultiplicatorTable, true, 4, \"\", 2, \"value\" = 1);\n",
    "MARKTABLE(SimpleFraction, 3, SpecialChararactorFractionTable, true, 4, \"\", 2, \"numerator\" = 1, \"denominator\" = 2);\n",
    "MARKTABLE(NumericValue, 2, SpecialCharacterTable, true, 4, \"\", 2,  \"value\" = 1);\n",
    "MARKTABLE(ExponentInd, 2, ExponentTable, true, 4, \"\", 2,  \"value\" = 1);\n",
    "\n",
    "ConjunctionWord{PARTOF(MultiplicatorInd)-> UNMARK(ConjunctionWord)};\n",
    "\n",
    "DOUBLE value;\n",
    "\n",
    "// normal numbers like 1,000.95\n",
    "RETAINTYPE(WS);\n",
    "(NUM{-PARTOF(NumericValue)} (ThousandsSeparator NUM{REGEXP(\"...\")})* (DecimalSeparator NUM)?)\n",
    "    {PARSE(value, language) -> CREATE(NumericValue, \"value\" = value)};\n",
    "W{-REGEXP(\"[ex]\", true)} @NumericValue{-> UNMARK(NumericValue)};\n",
    "RETAINTYPE;\n",
    "\n",
    "BLOCK(additionalPeriodSep) Document{IF(allowPeriodDecimalSeparator)} {\n",
    "    (NUM{IS(NumericValue)-> UNMARK(NumericValue)} PERIOD NUM{IS(NumericValue)-> UNMARK(NumericValue)})\n",
    "        {PARSE(value, \"en\") -> CREATE(NumericValue, \"value\" = value)};\n",
    "}\n",
    "\n",
    "// fractions like 3/4\n",
    "NumericValue{-> UNMARK(NumericValue)} SPECIAL{REGEXP(\"/\")} NumericValue{-> UNMARK(NumericValue), \n",
    "    GATHER(Fraction,1,3, \"numerator\" = 1, \"denominator\" = 3)};\n",
    "\n",
    "f:Fraction{-> CREATE(NumericValue, \"value\" = (f.numerator.value / f.denominator.value))};\n",
    "sf:SimpleFraction{-> CREATE(NumericValue, \"value\" = (sf.numerator / sf.denominator))};\n",
    "\n",
    "\n",
    "// exponents like 2^3, 2.3e13, 4²\n",
    "(NumericValue{-> ASSIGN(value, NumericValue.value), UNMARK(NumericValue)} \n",
    "    ExponentInd{-> ASSIGN(value, (POW(value, ExponentInd.value)))})\n",
    "    {-> CREATE(NumericValue, \"value\" = value)};\n",
    "\n",
    "(NumericValue{-> ASSIGN(value, NumericValue.value), UNMARK(NumericValue)} \n",
    "    SPECIAL{REGEXP(\"\\\\^\")}\n",
    "    NumericValue{-> ASSIGN(value, (POW(value, NumericValue.value))), UNMARK(NumericValue)})\n",
    "    {-> CREATE(NumericValue, \"value\" = value)};\n",
    "\n",
    "(NumericValue{-> ASSIGN(value, NumericValue.value), UNMARK(NumericValue)} \n",
    "    W{REGEXP(\"e\", true)}\n",
    "    NumericValue{-> ASSIGN(value, value * (POW(10, NumericValue.value))), UNMARK(NumericValue)})\n",
    "    {-> CREATE(NumericValue, \"value\" = value)};\n",
    "\n",
    "// multiplication like 3x4, 2*2\n",
    "\n",
    "(NumericValue{-> ASSIGN(value, NumericValue.value), UNMARK(NumericValue)} \n",
    "    ANY{REGEXP(\"x|\\\\*\", true)}\n",
    "    NumericValue{-> ASSIGN(value, (value * NumericValue.value)), UNMARK(NumericValue)})\n",
    "    {-> CREATE(NumericValue, \"value\" = value)};\n",
    "\n",
    "\n",
    "// combination with multipliers like 3 million\n",
    "(NumericValue{-> ASSIGN(value, NumericValue.value), UNMARK(NumericValue)} \n",
    "    SPECIAL?{REGEXP(\"-\"), NEAR(W,0,1,true)}\n",
    "   NumericValue?{-> ASSIGN(value, value + NumericValue.value), UNMARK(NumericValue)}\n",
    "   (\n",
    "       MultiplicatorInd{-> ASSIGN(value, value * (POW(10, MultiplicatorInd.value)))} \n",
    "       NumericValue?{-> ASSIGN(value, value + NumericValue.value), UNMARK(NumericValue)}\n",
    "   )*\n",
    "   ){-> CREATE(NumericValue, \"value\" = value)};\n",
    "\n",
    "// fünfundzwanzig\n",
    "(NumericValue{PARTOF(W)-> ASSIGN(value, NumericValue.value), UNMARK(NumericValue)} \n",
    "    @ConjunctionWord NumericValue.value!=0{PARTOF(W),IF((NumericValue.value%1) == 0)\n",
    "       -> ASSIGN(value, value + NumericValue.value), UNMARK(NumericValue)})\n",
    "       {-> CREATE(NumericValue, \"value\" = value)};"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "UIMA Ruta",
   "language": "ruta",
   "name": "ruta"
  },
  "language_info": {
   "codemirror_mode": "ruta",
   "file_extension": ".ruta",
   "mimetype": "text/ruta",
   "name": "UIMA Ruta",
   "pygments_lexer": "ruta",
   "version": "3.0.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
