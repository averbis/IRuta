{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Exercise 3: Sectioning\n",
    "\n",
    "The goal of this exercise if to create a simple rule script for segmenting documents by their headlines. The resulting annotation named \"Section\" should contain two features \"headline\" and \"content\". The \"headline\" feature should contain an annotation of the type \"Headline\", which specifies the headline of the section. The \"content\" feature should contains an annotation of the type \"Content\", which specifies the content of the section. The offsets of the \"Section\" annotation should cover the headline as well as the content.\n",
    "\n",
    "We declare the required types and specify rules that process all input documents of the project as correct as possible. Subsections, listings, tables, figure, header and footers are not considered."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%inputDir input_sectioning\n",
    "%displayMode CSV\n",
    "%csvConfig DocumentAnnotation\n",
    "\n",
    "ENGINE org.apache.uima.ruta.engine.PlainTextAnnotator;\n",
    "TYPESYSTEM org.apache.uima.ruta.engine.PlainTextTypeSystem;\n",
    "\n",
    "EXEC(PlainTextAnnotator, {Line, EmptyLine});\n",
    "DECLARE FreeLine, LineFree;\n",
    "RETAINTYPE(WS);\n",
    "EmptyLine Line{-> FreeLine};\n",
    "Line{-> LineFree} BREAK[1,2] @EmptyLine;\n",
    "Line{-> TRIM(WS)};\n",
    "FreeLine{-> TRIM(WS)};\n",
    "LineFree{-> TRIM(WS)};\n",
    "RETAINTYPE;\n",
    "\n",
    "DECLARE Headline, Content, HeadlineCandidate;\n",
    "DECLARE Section (Headline headline, Content content);\n",
    "\n",
    "FOREACH(line) Line {} {\n",
    "    line{STARTSWITH(CAP), ENDSWITH(CAP) -> HeadlineCandidate};\n",
    "    line{->HeadlineCandidate}<-{NUM{STARTSWITH(Line)} W+;};\n",
    "}\n",
    "\n",
    "DECLARE OverallFirst;\n",
    "MARKFIRST(OverallFirst);\n",
    "\n",
    "HeadlineCandidate{CONTAINS(SPECIAL,2,100) -> UNMARK(HeadlineCandidate)};\n",
    "HeadlineCandidate{CONTAINS(NUM,2,100) -> UNMARK(HeadlineCandidate)};\n",
    "HeadlineCandidate{ENDSWITH(PM), -ENDSWITH(COLON) -> UNMARK(HeadlineCandidate)};\n",
    "\n",
    "BLOCK(eachLine) HeadlineCandidate {} {\n",
    "    Document{-PARTOF(Headline), CONTAINS(CAP,100,100,true)-> Headline};\n",
    "    Document{IS(FreeLine), IS(LineFree) -> Headline};\n",
    "    Document{STARTSWITH(OverallFirst), IS(LineFree) -> Headline};\n",
    "}\n",
    "\n",
    "BOOLEAN hlNum;\n",
    "\n",
    "BLOCK(failedYet) Document{-CONTAINS(Headline)} {\n",
    "    HeadlineCandidate{REGEXP(\"\\\\d\\\\s*Introduction\") -> hlNum = true};\n",
    "    BLOCK(failedYet) Document{hlNum} {\n",
    "        HeadlineCandidate{-STARTSWITH(NUM) -> UNMARK(HeadlineCandidate)};\n",
    "        HeadlineCandidate{-> Headline};\n",
    "    }\n",
    "}\n",
    "\n",
    "\n",
    "(Headline #{-> Content})\n",
    "    {-> CREATE(Section,\n",
    "        \"headline\" = Headline, \n",
    "        \"content\" = Content)} Headline;\n",
    "\n",
    "(Headline{-PARTOF(Section)} #{-> Content})\n",
    "    {-> CREATE(Section,\n",
    "        \"headline\" = Headline, \n",
    "        \"content\" = Content)};\n",
    "\n",
    "COLOR(Headline, \"lightgreen\");"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "UIMA Ruta",
   "language": "ruta",
   "name": "ruta"
  },
  "language_info": {
   "codemirror_mode": "ruta",
   "file_extension": ".ruta",
   "mimetype": "text/ruta",
   "name": "UIMA Ruta",
   "pygments_lexer": "ruta",
   "version": "3.0.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
