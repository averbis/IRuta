{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Exercise 3: Sectioning\n",
    "\n",
    "The goal of this exercise is to create a simple rule script for segmenting documents by their headlines. The resulting annotation named `Section` should contain two features `headline` and `content`. The `headline` feature should contain an annotation of the type `Headline`, which specifies the headline of the section. The `content` feature should contain an annotation of the type `Content`, which specifies the content of the section. The offsets of the `Section` annotation should cover the headline as well as the content.\n",
    "\n",
    "We declare the required types and specify rules that process all input documents of the project as correct as possible. Subsections, listings, tables, figures, headers and footers are not considered."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%loadDocument data/ex3_sectioning/example1.txt\n",
    "// We encourage you to also try out example2.txt and example3.txt\n",
    "\n",
    "// Loading and applying the external PlainTextAnnotator for creating \"Line\" and \"EmptyLine\" annotations\n",
    "ENGINE org.apache.uima.ruta.engine.PlainTextAnnotator;\n",
    "TYPESYSTEM org.apache.uima.ruta.engine.PlainTextTypeSystem;\n",
    "EXEC(PlainTextAnnotator, {Line, EmptyLine});\n",
    "\n",
    "// Auxillary types: \n",
    "//   FreeLine (\"Free and then a Line\") is a Line after an empty (free) Line\n",
    "//   LineFree (\"Line and then a Free\") is a Line before an empty (free) Line\n",
    "DECLARE FreeLine, LineFree;\n",
    "RETAINTYPE(WS);\n",
    "EmptyLine Line{-> FreeLine};\n",
    "Line{-> LineFree} BREAK[1,2] @EmptyLine;\n",
    "Line{-> TRIM(WS)};\n",
    "FreeLine{-> TRIM(WS)};\n",
    "LineFree{-> TRIM(WS)};\n",
    "RETAINTYPE;\n",
    "\n",
    "DECLARE Headline, Content, HeadlineCandidate;\n",
    "DECLARE Section (Headline headline, Content content);\n",
    "\n",
    "// Candidates for Headlines are lines which start and end with fully capitalized words \n",
    "// or that start with a number\n",
    "FOREACH(line) Line {} {\n",
    "    line{STARTSWITH(CAP), ENDSWITH(CAP) -> HeadlineCandidate};\n",
    "    line{->HeadlineCandidate}<-{NUM{STARTSWITH(Line)} W+;};\n",
    "}\n",
    "\n",
    "// We discard Headline candidates with more than two special characters or numbers, \n",
    "// or those that end with a punctuation mark that is not a colon\n",
    "HeadlineCandidate{CONTAINS(SPECIAL,2,100) -> UNMARK(HeadlineCandidate)};\n",
    "HeadlineCandidate{CONTAINS(NUM,2,100) -> UNMARK(HeadlineCandidate)};\n",
    "HeadlineCandidate{ENDSWITH(PM), -ENDSWITH(COLON) -> UNMARK(HeadlineCandidate)};\n",
    "\n",
    "// OverallFirst is the first token in the complete document - it requires special treatment\n",
    "DECLARE OverallFirst;\n",
    "MARKFIRST(OverallFirst);\n",
    "\n",
    "// We convert HeadlineCandidates to Headline if one of the three conditions is satisfied\n",
    "BLOCK(eachLine) HeadlineCandidate {} {\n",
    "    // 1) The candidate is not already a Headline and contains ONLY fully capitalized words\n",
    "    Document{-PARTOF(Headline), CONTAINS(CAP,100,100,true)-> Headline};\n",
    "    \n",
    "    // 2) The candidate is preceded and followed by an empty Line\n",
    "    Document{IS(FreeLine), IS(LineFree) -> Headline};\n",
    "    \n",
    "    // 3) It is the first line in the document and followed by an empty Line\n",
    "    Document{STARTSWITH(OverallFirst), IS(LineFree) -> Headline};\n",
    "}\n",
    "\n",
    "// Fallback rule: \n",
    "// This is a specific rule for example3.txt where we did not annotate any Headlines with the above rules\n",
    "// In this case, we generate headlines based on numbering, e.g. \"1 Introduction\", \"2 Related Work\", etc.\n",
    "// will be annotated as Headlines\n",
    "BOOLEAN hlNum;\n",
    "BLOCK(failedYet) Document{-CONTAINS(Headline)} {\n",
    "    HeadlineCandidate{REGEXP(\"\\\\d\\\\s*Introduction\") -> hlNum = true};\n",
    "    BLOCK(failedYet) Document{hlNum} {\n",
    "        HeadlineCandidate{-STARTSWITH(NUM) -> UNMARK(HeadlineCandidate)};\n",
    "        HeadlineCandidate{-> Headline};\n",
    "    }\n",
    "}\n",
    "\n",
    "// Finally we generate Sections from one Headline to the next\n",
    "(Headline #{-> Content})\n",
    "    {-> CREATE(Section,\n",
    "        \"headline\" = Headline, \n",
    "        \"content\" = Content)} Headline;\n",
    "\n",
    "(Headline{-PARTOF(Section)} #{-> Content})\n",
    "    {-> CREATE(Section,\n",
    "        \"headline\" = Headline, \n",
    "        \"content\" = Content)};\n",
    "\n",
    "COLOR(Headline,\"lightgreen\");"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "UIMA Ruta",
   "language": "ruta",
   "name": "ruta"
  },
  "language_info": {
   "codemirror_mode": "ruta",
   "file_extension": ".ruta",
   "mimetype": "text/ruta",
   "name": "UIMA Ruta",
   "pygments_lexer": "ruta",
   "version": "3.1.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
